PREFIX express: <https://w3id.org/express#>
PREFIX list: <https://w3id.org/list#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ifc:     <http://standards.buildingsmart.org/IFC/DEV/IFC4/ADD2_TC1/OWL#>
# TODO: INCLUDE PROPUNITURI SEE IFC2_3 FOR GUIDANCE
SELECT DISTINCT ?propName ?propType ?valueType ?propValue ?enumOptionValue ?propUnitUri WHERE {
    {
        ?uri
            ifc:nominalValue_IfcPropertySingleValue ?propValueUri ;
            ifc:name_IfcProperty ?labelUri .
        ?labelUri express:hasString ?propName .
        ?propValueUri
            a ?propType ;
            express:hasString|express:hasBoolean|express:hasDouble|express:hasInteger ?propValue ;
            ?valueType ?propValue .
    }
    UNION
    {
        ?uri
            a ifc:IfcQuantityLength ;
            ifc:name_IfcPhysicalQuantity ?labelUri ;
            ifc:lengthValue_IfcQuantityLength ?measureUri .
        ?labelUri express:hasString	?propName .
        ?measureUri
            express:hasString|express:hasBoolean|express:hasDouble|express:hasInteger ?propValue ;
            ?valueType ?propValue ;
            rdf:type ?propType .
    }
    UNION
    {
        ?uri
            a ifc:IfcQuantityVolume ;
            ifc:name_IfcPhysicalQuantity ?labelUri ;
            ifc:volumeValue_IfcQuantityVolume ?measureUri .
        ?labelUri express:hasString	?propName .
        ?measureUri
            express:hasString|express:hasBoolean|express:hasDouble|express:hasInteger ?propValue ;
            ?valueType ?propValue ;
            rdf:type ?propType .
    }
    UNION
    {
        ?uri
            a ifc:IfcQuantityArea ;
            ifc:name_IfcPhysicalQuantity ?labelUri ;
            ifc:areaValue_IfcQuantityArea ?measureUri .
        ?labelUri express:hasString	?propName .
        ?measureUri
            express:hasString|express:hasBoolean|express:hasDouble|express:hasInteger ?propValue ;
            ?valueType ?propValue ;
            rdf:type ?propType .
    }
    UNION
    {
        ?uri
            ifc:enumerationValues_IfcPropertyEnumeratedValue ?propValueUri ;
            ifc:name_IfcProperty ?labelUri .
        ?labelUri express:hasString ?propName .
        ?propValueUri
            a ?propType ;
            list:hasContents ?enumValueUri .
        ?enumValueUri
            express:hasString|express:hasBoolean|express:hasDouble|express:hasInteger ?propValue ;
            ?valueType ?propValue .
    }
    UNION
    {
        ?uri
            ifc:enumerationValues_IfcPropertyEnumeratedValue ?propValueUri ;
            ifc:enumerationReference_IfcPropertyEnumeratedValue ?enumRefUri ;  # --> IFC Reference (to extract all values)
            ifc:name_IfcProperty ?labelUri .
        ?labelUri express:hasString ?propName .
        ?enumRefUri ifc:enumerationValues_IfcPropertyEnumeration/list:hasNext* ?enumOptionUri .
        ?enumOptionUri
            list:hasContents ?enumOptionPropUri ;
            list:hasNext ?nextEnumOptionUri .
        ?enumOptionPropUri express:hasString ?enumOptionValue .
        ?propValueUri
            a ?propType ;
            list:hasContents ?enumValueUri .
        ?enumValueUri
            express:hasString|express:hasBoolean|express:hasDouble|express:hasInteger ?propValue ;
            ?valueType ?propValue .
    }
}
