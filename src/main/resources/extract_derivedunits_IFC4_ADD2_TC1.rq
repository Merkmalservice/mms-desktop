PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ifc:     <http://standards.buildingsmart.org/IFC/DEV/IFC4/ADD2_TC1/OWL#>
PREFIX express: <https://w3id.org/express#>
SELECT DISTINCT ?projectUri ?unitAssignmentUri ?unitUri ?derivedUnitType ?userDefinedTypeLabel ?derivedUnitElementUri ?exponentValue ?derivedUnitElMeasure ?derivedUnitElPrefix ?derivedUnitElType ?derivedUnitElUri
WHERE {
      ?unitUri a ifc:IfcDerivedUnit .
      ?unitUri ifc:unitType_IfcDerivedUnit ?derivedUnitType .
      OPTIONAL { ?unitUri ifc:userDefinedType_IfcDerivedUnit ?userDefinedTypeUri .
                 ?userDefinedTypeUri express:hasString ?userDefinedTypeLabel .}
      ?unitUri ifc:elements_IfcDerivedUnit ?derivedUnitElementUri .
      ?derivedUnitElementUri ifc:exponent_IfcDerivedUnitElement ?exponentUri .
      ?derivedUnitElementUri ifc:unit_IfcDerivedUnitElement ?derivedUnitElUri .
      ?exponentUri express:hasInteger ?exponentValue .
      ?derivedUnitElUri ifc:name_IfcSIUnit ?derivedUnitElMeasure .
      ?derivedUnitElUri ifc:unitType_IfcNamedUnit ?derivedUnitElType.
      OPTIONAL { ?derivedUnitElUri ifc:prefix_IfcSIUnit ?derivedUnitElPrefix . }
      OPTIONAL {
               ?projectUri a ifc:IfcProject .
               ?projectUri ifc:unitsInContext_IfcContext ?unitAssignmentUri .
               ?unitAssignmentUri ifc:units_IfcUnitAssignment ?unitUri .
               }

      }
